import java.util.regex.Pattern

apply plugin: 'com.android.library'
apply plugin: 'maven'

apply from: project.file("version.gradle")

ext.pomProjectName = 'Layer XDK'
ext.artifactIdName = 'xdk'
group 'com.layer.xdk'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName = "${version}"
        vectorDrawables.useSupportLibrary = true

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            // Only enable multidex for debug builds as test libraries pushes it over the limit
            multiDexEnabled true
        }
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        // Seems to be a conflict between Espresso 3.0.1 and Firebase 11.8.0
        pickFirst 'protobuf.meta'
    }

    // Data binding is still trying to use 25.2.0 for some reason
    configurations.all {
        resolutionStrategy.force 'androidx.legacy:legacy-support-v4:1.0.0'
    }
}

repositories {
    maven { url "https://raw.githubusercontent.com/layerhq/releases-android/master/releases/" }
    jcenter()
    maven { url "https://maven.google.com" }
}

ext {
    org = "layerhq"
    repo = "Android-XDK"
    groupFolder = "com/layer/xdk"
}

dependencies {
    // Layer SDK
    api ('com.layer.sdk:layer-sdk:' + rootProject.ext.layerSdkVersion) {
        exclude group: 'com.google.firebase', module: 'firebase-messaging'
        exclude group: 'com.google.firebase', module: 'firebase-core'
    }
    implementation 'org.slf4j:slf4j-nop:1.7.12'
    // Google Location
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    // Image cache and loading
    implementation 'com.squareup.picasso:picasso:2.71828'
    // Image popup
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.9.0'
    // Dependency injection
    implementation 'com.google.dagger:dagger-android:' + rootProject.ext.daggerVersion
    annotationProcessor 'com.google.dagger:dagger-compiler:' + rootProject.ext.daggerVersion

    // Android UI
    // Keep as api for databinding
    api 'androidx.legacy:legacy-support-core-utils:' + rootProject.ext.xlegacySupportCoreUtils
    implementation 'androidx.recyclerview:recyclerview:'  + rootProject.ext.xRecyclerview
    api 'androidx.appcompat:appcompat:' + rootProject.ext.xAppcompat
    implementation 'com.google.android.material:material:' + rootProject.ext.xMaterial
    // Keep as api for views that expose this layout
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.exifinterface:exifinterface:' + rootProject.ext.xExifinterface
    // Keep as api so MessageItemsListViewModel can be used
    api "androidx.paging:paging-runtime:2.1.0"
    implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
    // Include only for debug builds (namely for tests)
    debugImplementation 'androidx.multidex:multidex:' + rootProject.ext.xMultidex
    // Testing
    testImplementation project(':xdk-test-common')
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:' + rootProject.ext.mockitoVersion
    testImplementation 'org.robolectric:robolectric:4.1'
    androidTestImplementation project(':xdk-test-common')
    androidTestImplementation 'org.mockito:mockito-core:' + rootProject.ext.mockitoVersion
    androidTestImplementation 'androidx.test:runner:' + rootProject.ext.runnerVersion
    androidTestImplementation 'androidx.test:rules:' + rootProject.ext.rulesVersion
    androidTestImplementation 'com.linkedin.testbutler:test-butler-library:1.3.1'
    androidTestImplementation 'com.google.dagger:dagger-android:' + rootProject.ext.daggerVersion
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:' + rootProject.ext.daggerVersion

    // Espresso support
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:' + rootProject.ext.espressoVersion
    androidTestImplementation('androidx.test.espresso:espresso-core:' + rootProject.ext.espressoVersion, {
        exclude group: 'androidx.annotation', module: 'annotation'
    })
    testImplementation 'com.google.truth:truth:' + rootProject.ext.truthVersion
    androidTestImplementation 'com.google.truth:truth:' + rootProject.ext.truthVersion
}

def sdkPom() {
    pom {
        project {
            name = pomProjectName
            packaging "aar"
            artifactId = artifactIdName

            developers {
                developer {
                    name 'Amar Srinivasan'
                    email 'amar@layer.com'
                }
                developer {
                    name 'Peter Elliott'
                    email 'peter@layer.com'
                }
            }
        }
    }
}

public String url() {
    return "file://$rootDir/releases"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    title = "Layer XDK UI"

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task publishAar(type: Upload, description: "Upload android AAR library") {
    configuration = configurations.archives
    uploadDescriptor = true
    repositories {
        mavenDeployer {
            repository(url: url())
            pom = sdkPom()
        }
    }
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

// Tasks for publishing a release to GitHub

def getReleaseNotes(version) {
    def capturing = false
    def sb = new StringBuilder();
    new File('./CHANGELOG.md').eachLine { line ->
        if (line ==~ /^\#\#\s${Pattern.quote(version)}$/) {
            capturing = true
        } else if (line ==~ /^\#\#\s[\d\.]+$/) {
            capturing = false
        } else if (capturing) {
            sb.append(line + "\n")
        }
    }

    // Trim and escape backticks
    return sb.toString()
            .trim()
            .replaceAll("`", "\\\\`")
}

def getGitHubToken() {
    return hasProperty('GITHUB_TOKEN') ? GITHUB_TOKEN : "";
}

def getUploadCommand(filename) {
    return "tools/github-release upload " +
            "--security-token ${getGitHubToken()} " +
            "--user $org " +
            "--repo $repo " +
            "--tag $version " +
            "--name $filename " +
            "--file releases/$groupFolder/$artifactIdName/$version/$filename"
}

afterEvaluate { project ->
    // Only add publishing tasks if preconditions are met so this can be used as a submodule
    if (!getGitHubToken().isEmpty() && new File('./CHANGELOG.md').exists()) {
        task uploadReleaseNotes(type: Exec) {
            workingDir "$rootDir"
            commandLine "bash", "-c", "tools/github-release release " +
                    "--security-token ${getGitHubToken()} " +
                    "--user $org " +
                    "--repo $repo " +
                    "--tag $version " +
                    "--description \"${getReleaseNotes("$version")}\""
        }

        task uploadReleaseAar(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}.aar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task uploadReleaseSources(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}-sources.jar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task uploadReleaseJavadoc(type: Exec, dependsOn: [uploadReleaseNotes, publishAar]) {
            workingDir "$rootDir"

            def filename = "$artifactIdName-${version}-javadoc.jar"
            commandLine "bash", "-c", getUploadCommand(filename)
        }

        task publishReleaseToGitHub(dependsOn: [uploadReleaseNotes, uploadReleaseAar, uploadReleaseSources, uploadReleaseJavadoc])
    }
}
